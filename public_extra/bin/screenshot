#!/usr/bin/env fish

# requires https://github.com/naelstrof/maim

set -g screen_dir "$HOME/pictures/screenshots"
set -g remote_host 'https://image.home.senan.xyz'
set -g maim_options

if contains 'select' $argv
    notify-send 'screenshot' 'select an area'
    set -a maim_options '--select'
    set -a maim_options '--bordersize=3'
end

set -g filename "$screen_dir/"(date '+%Y%m%d_%H%M%S.png')
maim $maim_options "$filename"
notify-send 'screenshot' 'taken '(basename $filename)

# add to socr database
# see https://github.com/sentriz/socr
socr add "$filename" &

contains 'upload' $argv; or exit
set -l credentials (printf \
    '%s:%s' \
    (pass boxes/hog/image/username) \
    (pass boxes/hog/image/password)
)
set -l remote (curl \
    "$remote_host/upload" \
    --user "$credentials" \
    --form "img=@$filename"
)
echo "$remote_host/$remote" | xclip -i
notify-send 'screenshot' 'link in clipboard'
