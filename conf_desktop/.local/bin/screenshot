#!/usr/bin/env fish

echo $argv | read src dest
test -z $src; and exit 1
test -z $dest; and exit 1

set filename $DOTS_SCREENSHOTS_DIR/(date '+%Y%m%d_%H%M%S.png')

switch $src
    case clipboard
        wl-paste -l | grep -q "^image/png"
        and wl-paste >$filename
    case "*"
        grim -g (area $src) $filename
end
test -f $filename; or exit 1

function alert_copy
    read link
    test -z $link; and return 1
    wl-copy $link
    notify-send screenshot "uploaded to $argv[1]"
end

switch $dest
    case home
        upload-home $filename | alert_copy home
    case imgur
        upload-imgur $filename | alert_copy imgur
    case ptp
        upload-ptp $filename | alert_copy ptp
    case x0
        upload-x0 $filename | alert_copy x0
    case clipboard
        wl-copy <$filename
    case drop
        drop $filename
end
