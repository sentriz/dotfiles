#!/usr/bin/env fish

if not type -q grim /dev/null 2>&1
    echo 'grim required' >&2
    echo 'https://github.com/emersion/grim' >&2
    exit 1
end

if not type -q slurp >/dev/null 2>&1
    echo 'slurp required' >&2
    echo 'https://github.com/emersion/slurp' >&2
    exit 1
end

if not type -q pactl >/dev/null 2>&1
    echo 'pactl required' >&2
    echo 'please install libpulse' >&2
    exit 1
end

set -l command "$argv[1]"
if test "$command" = stop
    killall --signal SIGINT wf-recorder
    exit
end

set -l filename "$DOTS_RECORDINGS_DIR/"(date '+%Y%m%d_%H%M%S.mp4')

contains -- --monitor $argv
and set -l slurp_cmd -o

set -l area (slurp $slurp_cmd)
test -z "$area"; and exit

set -l audio_device (pactl list short sources | grep -Po "\K[^\s]+\.monitor(?=.*IDLE)")
test -n "$audio_device"
and contains -- --audio $argv
and set -l audio_cmd "-a$audio_device"

wf-recorder $audio_cmd -f "$filename" -g "$area" --force-yuv >/dev/null 2>&1
echo "$filename"
