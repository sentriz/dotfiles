#!/usr/bin/env fish

# senan kelly, 2020

if test (count $argv) -ne 1
    echo "usage: upload-home <path to image>" >&2
    exit 1
end

set -l image_path $argv[1]
set -l url_upload (secret image_url_upload)
set -l url_public (secret image_url_public)
set -l api_key (secret image_api_key)

if ! test -e "$image_path"
    echo "couldn't find image $image_path" >&2
    exit 1
end

set -l image_id (
    curl -s "$url_upload" \
        --header (printf "X-API-Key: %s" $api_key) \
        --form "image=@$image_path" \
    | jq -r '.id'
)

if test -n "$image_id"
    echo "$url_public/$image_id"
    isatty stdout || notify-send.sh 'screenshot' 'uploaded to home' >/dev/null 2>&1
    exit
end
